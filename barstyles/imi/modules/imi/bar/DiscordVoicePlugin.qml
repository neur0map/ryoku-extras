pragma ComponentBehavior: Bound

import QtQuick
import QtQuick.Layouts
import "../../../services"
import "../../common"
import "../../common/widgets"
import "../../common/plugins"
import "../../common/plugins/bundled/discordVoice" as DiscordPackage

MouseArea {
    id: root
    property bool vertical: Config.options.bar.vertical
    property bool popupOpen: false
    readonly property int avatarLimit: PluginState.option("discord_voice", "maxBarAvatars", 4)

    implicitWidth: vertical ? 34 : content.implicitWidth + Appearance.spacing.space100 * 2
    implicitHeight: vertical ? content.implicitHeight + Appearance.spacing.space50 * 2 : Appearance.sizes.barHeight
    acceptedButtons: Qt.LeftButton
    hoverEnabled: false
    cursorShape: Qt.PointingHandCursor
    onClicked: root.popupOpen = !root.popupOpen

    RowLayout {
        id: content
        anchors.centerIn: parent
        spacing: Appearance.spacing.space50
        DiscordPackage.DiscordGlyph {
            implicitSize: 29
            iconSize: 17
            color: DiscordVoice.inVoice
                ? Appearance.colors.colPrimaryContainer : Appearance.colors.colLayer2
            iconColor: DiscordVoice.inVoice
                ? Appearance.colors.colOnPrimaryContainer : Appearance.colors.colSubtext
        }
        Row {
            visible: !root.vertical && DiscordVoice.participantCount > 0
            spacing: -Appearance.spacing.space50
            Repeater {
                model: DiscordVoice.participantModel
                DiscordPackage.ParticipantAvatar {
                    // Bound component behavior does not inject `index` into the
                    // delegate's scope; the overlapping avatar stack needs it to
                    // order itself, so it has to be taken as a required property.
                    required property int index
                    visible: index < root.avatarLimit
                    avatarSize: 25
                    z: index
                }
            }
        }
        StyledText {
            visible: !root.vertical && DiscordVoice.inVoice
            text: DiscordVoice.participantCount
            font.pixelSize: Appearance.font.pixelSize.small
            font.weight: Font.DemiBold
            color: Appearance.colors.colOnPrimaryContainer
        }
    }

    Loader {
        id: popupLoader
        active: root.popupOpen
        sourceComponent: DiscordPackage.DiscordVoicePopup {
            pinnedOpen: true
            hoverTarget: root
            // The overlay owns the surface, so it owns the outside-click grab.
            onDismissRequested: root.popupOpen = false
            onPinnedOpenChanged: if (!pinnedOpen) root.popupOpen = false
        }
    }
}
